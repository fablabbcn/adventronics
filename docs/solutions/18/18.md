# Day eighteen:

Connect to an API to get the weather, the temperatue or anythig else and visualize it using your Barduino. 

You can try to use the [SmartCitizen API](https://developer.smartcitizen.me/)

## Possible solution!

### Arduino code

```c++
#include <Arduino.h>
#include <WiFi.h>
#include <WiFiClientSecure.h>
#include <HTTPClient.h>
#include <Arduino_JSON.h>
#include <Adafruit_NeoPixel.h>

#define PIN 38
#define NUMPIXELS 1

Adafruit_NeoPixel pixels(NUMPIXELS, PIN, NEO_GRB + NEO_KHZ800);

// Replace with your network credentials
const char* ssid = "yournetwork";
const char* password = "yourpassword";

const char* serverName = "https://api.smartcitizen.me/v0/devices/16866/readings?sensor_id=55&rollup=1h";

const int but = 0;

bool pressed = false;

double temp;

void setup() {
  pinMode(but, INPUT);
  pixels.begin();
  pixels.setBrightness(100);
  Serial.begin(115200);
  Serial.println();
  // Initialize Wi-Fi
  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);
  Serial.print("Connecting to WiFi ..");
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print('.');
    delay(1000);
  }
  Serial.println(WiFi.localIP());
}

void loop() {

  if (!digitalRead(but) && !pressed) {
    pressed = true;
    WiFiClientSecure* client = new WiFiClientSecure;
    if (client) {
      // set secure client without certificate
      client->setInsecure();
      //create an HTTPClient instance
      HTTPClient https;

      //Initializing an HTTPS communication using the secure client
      Serial.print("[HTTPS] begin...\n");
      if (https.begin(*client, serverName)) {  // HTTPS
        Serial.print("[HTTPS] GET...\n");
        // start connection and send HTTP header
        int httpCode = https.GET();
        // httpCode will be negative on error
        if (httpCode > 0) {
          // HTTP header has been send and Server response header has been handled
          Serial.printf("[HTTPS] GET... code: %d\n", httpCode);
          // file found at server
          if (httpCode == HTTP_CODE_OK || httpCode == HTTP_CODE_MOVED_PERMANENTLY) {
            // print server response payload
            String payload = https.getString();
            //Serial.println(payload);
            JSONVar myObject = JSON.parse(payload);

            if (JSON.typeof(myObject) == "undefined") {
              Serial.println("Parsing input failed!");
            }

            temp = myObject["readings"][0][1];
            Serial.print("Temperature = ");
            Serial.print(temp);
            Serial.print("  Measured: ");
            Serial.println(myObject["readings"][0][0]);

            if (temp < 10){
              pixels.setPixelColor(0,pixels.Color(0,0,255));
            }
            else if (temp < 15){
              pixels.setPixelColor(0,pixels.Color(0,255,255));
            }
            else if (temp < 20){
              pixels.setPixelColor(0,pixels.Color(0,255,0));
            }
            else if (temp < 25){
              pixels.setPixelColor(0,pixels.Color(255,255,0));
            }
            else if (temp < 20){
              pixels.setPixelColor(0,pixels.Color(255,0,0));
            }
            pixels.show();

          }
        } else {
          Serial.printf("[HTTPS] GET... failed, error: %s\n", https.errorToString(httpCode).c_str());
        }
        https.end();
      }
    } else {
      Serial.printf("[HTTPS] Unable to connect\n");
    }
  }
  else if (digitalRead(but) && pressed){
    pressed = false;
  }
}
```

## Hero shot

<video controls autoplay loop style="display: block; margin: auto;">
    <source src="../../../video/day18.mp4" type="video/mp4">
</video>